; Console/Shell
; very simple console/shell, allowing to type commands
; and run modules

:console_prompt dat "> ", 0
:command_exit dat "exit", 0
:input dat "THIS TEXT IS TO BE OVERRIDDEN", 0

; entry procedure
; word (word context, char* command, char* parameters)
:module_console
     ifn [context], 0        ; if not run from the "init" context
         set pc, console_endbad ; end, setting result to 0

     set push, i
     set push, j
     set push, x
     set push, y
     set push, z

     jsr clrscr
     set i, modules

     set [context], 1
:console_loop
     set a, console_prompt
     jsr print

     set a, input
     jsr readline

     set a, command_exit
     set b, input
     jsr strcmp
     ife a, 1
          set pc, console_end

     ; iterate through modules
     set i, modules
:console_modules_loop
     set a, input

     ife [i], 0 ; if we've reached end, go to the
                ; next console module
          set pc, console_modules_loop_end

     ifn [i], 0 ; if module is set, jump to it
          jsr [i]

     ife a, 1   ; if module returned true, command was
                ; executed
          set pc, console_modules_loop_end

     add i, 1 ; go to next module
     set pc, console_modules_loop

:console_modules_loop_end

     ifn a, 1 ; if no module set this to 1, command was
              ; unrecognized
          jsr console_unrecognized


     set pc, console_loop

:console_endbad
     set a, 0 ; returning 0
     set pc, pop

:console_end
     set pop, z
     set pop, y
     set pop, x
     set pop, j
     set pop, i
     set pc, pop

:console_unrecognized
     jsr newline
     set a, munrecognized
     jsr print
     jsr newline
     set pc, pop

:munrecognized dat "Command not recognized", 0